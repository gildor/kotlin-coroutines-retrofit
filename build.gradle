plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.3-2"
    id "com.jfrog.bintray" version "1.7.3" apply false
    id "jacoco"
}

group 'ru.gildor.coroutines'
version '0.7.1'

repositories {
    jcenter()
}

targetCompatibility = '1.6'
sourceCompatibility = '1.6'

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${plugins.findPlugin("kotlin").properties["kotlinPluginVersion"]}"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.17"
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    testCompile 'junit:junit:4.12'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

tasks["check"].finalizedBy("jacocoTestReport")

apply from: "publish.gradle"
